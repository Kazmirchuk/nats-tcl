# Copyright (c) 2020 Petro Kazmirchuk https://github.com/Kazmirchuk

# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

namespace import ::tcltest::*
tcltest::configure {*}$argv

test_utils::startNats NATS

set conn [nats::connection new]
$conn configure -servers nats://localhost:4222

set responder [test_utils::responder new service]

test basic-1 "Connect NATS" -body {
    $conn connect -async
    set obs [test_utils::chanObserver new [set ${::conn}::sock] w]
    test_utils::sleep 500
    puts -nonewline [$obs getChanData]
    $obs destroy
    expr {[$conn cget -status] == $nats::status_connected}
} -result {1} -output {CONNECT {"verbose":false,"pedantic":false,"tls_required":false,"name":"","lang":"Tcl","version":"0.9","protocol":1,"echo":true}}

test basic-2 "Subscribe to a message" -body {
    set obs [test_utils::chanObserver new [set ${::conn}::sock] w]
    set sub_id [$conn subscribe subject1 [list test_utils::simpleCallback]]
    test_utils::sleep 1000
    puts -nonewline [$obs getChanData]
    $obs destroy
    set sub_id
}  -result {1} -output {SUB subject1  1}

test basic-3 "Publish a message" -body {
    set obs [test_utils::chanObserver new [set ${::conn}::sock] w]
    $conn publish subject1 hello
    vwait test_utils::simpleMsg
    puts -nonewline [$obs getChanData]
    $obs destroy
    set test_utils::simpleMsg
} -result {hello} -output {PUB subject1  5}

test basic-4 "Unsubscribe and publish again" -body {
    set obs [test_utils::chanObserver new [set ${::conn}::sock] w]
    $conn unsubscribe $sub_id
    $conn publish subject1 "one more hello"
    test_utils::sleep 500
    puts -nonewline [$obs getChanData]
    $obs destroy
    set test_utils::simpleMsg
} -result {hello} -output "UNSUB $sub_id"

test basic-5.1 "Synchronous request" -body {
    set obs [test_utils::chanObserver new [set ${::conn}::sock] b]
    set result [$conn request service "0 hello"]
    lassign [$obs getChanData 0] rdata wdata
    $obs destroy
    set result
} -result {hello} 

test basic-5.2 "Synchronous request - written to socket" -body {
    lassign [split $wdata \n] sub pub payload
    # the first request starts with subscribing to INBOX.<random>.*
    set check1 [regexp {SUB _INBOX.*.\*  ([[:digit:]]+)} $sub -> subID]
    set check2 [regexp {PUB service _INBOX.*.0 (.*)} $pub -> msg_length]
    set check3 [string equal $payload "0 hello"]
    set check4 [expr {$msg_length == [string length $payload]}]
    expr {$check1 && $check2 && $check3 && $check4}
} -result {1} 

test basic-5.3 "Synchronous request - read from socket" -body {
    lassign [split $rdata \n] msg payload
    set check1 [regexp {MSG _INBOX.*.0 ([[:digit:]]+) (.*)} $msg -> subID2 msg_length]
    set check2 [string equal $payload hello]
    set check3 [expr {$msg_length == [string length $payload]}]
    set check4 [expr {$subID == $subID2}]
    expr {$check1 && $check2 && $check3 && $check4}
} -result {1} 

test basic-5.4 "Synchronous request with timeout - success" -body {
    set result [$conn request service "500 sync_req" -timeout 600]
    set result
} -result {sync_req} 

test basic-5.5 "Synchronous request with timeout - failure" -body {
    set result [$conn request service "600 sync_req" -timeout 500]
} -returnCodes {error} -result {Request timeout}

# wait until the delayed message arrives
test_utils::sleep 500

test basic-6.1 "Asynchronous request" -body {
    $conn request service "0 async_req" -callback [list test_utils::asyncReqCallback]
    vwait test_utils::simpleMsg
    set test_utils::simpleMsg
} -result {async_req}

test basic-6.2 "Asynchronous request with timeout - success" -body {
    #TODO: reduce timeout... force flush in responder not working?
    $conn request service "0 async_req2" -timeout 1000 -callback [list test_utils::asyncReqCallback]
    vwait test_utils::simpleMsg
    set test_utils::simpleMsg
} -result {async_req2}

test basic-6.3 "Asynchronous request with timeout - failure" -body {
    $conn request service "500 async_req3" -timeout 400 -callback [list test_utils::asyncReqCallback]
    vwait test_utils::simpleMsg
    set test_utils::simpleMsg
} -result {timeout}

$responder destroy
$conn destroy
test_utils::stopNats NATS
cleanupTests
