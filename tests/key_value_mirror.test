# Copyright (c) 2023 Petro Kazmirchuk https://github.com/Kazmirchuk
# Copyright (c) 2023 ANT Solutions https://antsolutions.eu/

# Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the License for the specific language governing permissions and  limitations under the License.

# Test summary:
# -
# -


source test_utils.tcl
set hub_bucket MY_HUB_BUCKET
set mirror_bucket MY_MIRROR_BUCKET
set leaf_mirror_bucket MY_LEAF_MIRROR_BUCKET

startNats NATS_HUB -c ./conf/hub.conf
startNats NATS_LEAF -c ./conf/leaf.conf

# connect to the hub server and create a bucket
set conn_hub [nats::connection new "MyNatsToHub"]
$conn_hub configure -servers nats://localhost:4222 -user acc -password acc
$conn_hub connect
set js_hub [$conn_hub jet_stream]
set kv_hub [$js_hub create_kv_bucket $hub_bucket -history 64]

# connect to the leaf server
set conn_leaf [nats::connection new "MyNatsToLeaf"]
$conn_leaf configure -servers nats://localhost:4111 -user acc -password acc
$conn_leaf connect
set js_leaf [$conn_leaf jet_stream]

# connect to the leaf server but use the "hub" domain
set js_leaf_to_hub [$conn_leaf jet_stream -domain hub]

########### HUB <-> LEAF ###########
set kv_leaf_to_hub [$js_leaf_to_hub bind_kv_bucket $hub_bucket]

test key_value-domain-1 "Get values from the hub bucket using a leaf connection" -body {
    $kv_hub put key1 value1
    $kv_leaf_to_hub get key1
} -match glob -result [dict create bucket $hub_bucket key key1 value value1 revision 1 created * delta {} operation PUT]

test key_value-domain-2 "Put values to the hub bucket using a leaf connection" -body {
    $kv_leaf_to_hub put key1 value2
    $kv_hub get key1
} -match glob -result [dict create bucket $hub_bucket key key1 value value2 revision 2 created * delta {} operation PUT]

test key_value_domain-3 "Get history of the hub bucket using a leaf connection" -body {
    set entries [$kv_leaf_to_hub history key1]
    assert {[llength $entries] == 2} 1
    assert {[dict_in [dict create \
                      bucket $hub_bucket \
                      key key1 \
                      value value1 \
                      revision 1 \
                      operation PUT] [lindex $entries 0]]}
    assert {[dict_in [dict create \
                      bucket $hub_bucket \
                      key key1 \
                      value value2 \
                      revision 2 \
                      operation PUT] [lindex $entries 1]]}
        
} -result ""

test key_value_domain-4 "List hub buckets using a leaf connection" -body {
    $js_leaf_to_hub kv_buckets
} -result $hub_bucket

$kv_leaf_to_hub destroy
$js_hub empty_kv_bucket $hub_bucket

########### MIRROR IN THE SAME DOMAIN ###########

# note that we need to repeat -history 64 here - default history would be 1 irrespectively of the origin bucket
set kv_mirror [$js_hub create_kv_bucket $mirror_bucket -mirror_name $hub_bucket -history 64]
$kv_hub put key1 value3
sleep 100 ;# allow NATS to propagate values

test key_value-mirror-same-domain-1 "Create a KV mirror in the same domain and get a value" -body {
    $kv_mirror get key1
    # TODO check if bucket must be the original name or mirror name here:
} -match glob -result [dict create bucket $mirror_bucket key key1 value value3 revision * created * delta {} operation PUT]

test key_value-mirror-same-domain-2 "Put a value to the mirror and check the origin kv" -body {
    $kv_mirror put key1 value4
    sleep 100
    $kv_hub get key1
} -match glob -result [dict create bucket $hub_bucket key key1 value value4 revision * created * delta {} operation PUT]

test key_value-mirror-same-domain-3 "Get history from the mirror" -body {
    set entries [$kv_mirror history key1]
    llength $entries
} -result 2

$kv_mirror destroy

stopNats NATS_LEAF
stopNats NATS_HUB

tcltest::cleanupTests
return
# ########### MIRROR FROM OTHER DOMAIN ###########

test key_value-mirror-different-domain-1 "Create kv mirror in different domain and check its messages" -setup {
    execNatsCmd kv add $hub_bucket {*}$hub_auth
    execNatsCmd kv put $hub_bucket key1 value1 {*}$hub_auth
} -body {
    set kv_leaf [$js_leaf create_kv_bucket $leaf_mirror_bucket -mirror_name $hub_bucket -mirror_domain "hub"]
    after 100 ;# wait some time to give nats time to propagate messages
    set entry [$kv_leaf get key1]
    sortAndCheckEntries [list $entry]
} -result [list [dict create \
    bucket $hub_bucket \
    key key1 \
    operation PUT \
    revision 1 \
    value value1 \
]] -cleanup {
    execNatsCmd kv del $leaf_mirror_bucket -f {*}$leaf_auth
    execNatsCmd kv del $hub_bucket -f {*}$hub_auth
}

test key_value-mirror-different-domain-2 "Put message to mirror bucket and check if it is set in origin kv (hub domain) and mirror" -setup {
    execNatsCmd kv add $hub_bucket {*}$hub_auth
    execNatsCmd kv add $leaf_mirror_bucket --mirror $hub_bucket --mirror-domain "hub" {*}$leaf_auth
} -body {
    set kv_hub [$js_hub bind_kv_bucket $hub_bucket]
    set kv_leaf [$js_leaf bind_kv_bucket $leaf_mirror_bucket]
    $kv_leaf put key1 value1

    set entry_hub [$kv_hub get key1]
    set entry_mirror [$kv_leaf get key1]
    assert {$entry_hub == $entry_mirror}
    sortAndCheckEntries [list $entry_mirror]
} -result [list [dict create \
    bucket $hub_bucket \
    key key1 \
    operation PUT \
    revision 1 \
    value value1 \
]] -cleanup {
    execNatsCmd kv del $leaf_mirror_bucket -f {*}$leaf_auth
    execNatsCmd kv del $hub_bucket -f {*}$hub_auth
}

test key_value-mirror-different-domain-3 "Get history from mirror bucket" -setup {
    execNatsCmd kv add $hub_bucket {*}$hub_auth
    execNatsCmd kv put $hub_bucket key1 value1 {*}$hub_auth
    execNatsCmd kv put $hub_bucket key2 value2 {*}$hub_auth
    execNatsCmd kv add $leaf_mirror_bucket --mirror $hub_bucket --mirror-domain "hub" {*}$leaf_auth
} -body {
    set kv_leaf [$js_leaf bind_kv_bucket $leaf_mirror_bucket]
    set entries [$kv_leaf history]
    sortAndCheckEntries $entries
} -result [list [dict create \
    bucket $hub_bucket \
    key key1 \
    operation PUT \
    revision 1 \
    value value1 \
] [dict create \
    bucket $hub_bucket \
    key key2 \
    operation PUT \
    revision 2 \
    value value2 \
]] -cleanup {
    execNatsCmd kv del $leaf_mirror_bucket -f {*}$leaf_auth
    execNatsCmd kv del $hub_bucket -f {*}$hub_auth
}

test key_value-mirror-different-domain-4 "Bucket status should point to mirrored bucket" -setup {
    execNatsCmd kv add $hub_bucket {*}$hub_auth
    execNatsCmd kv add $leaf_mirror_bucket --mirror $hub_bucket --mirror-domain "hub" {*}$leaf_auth
} -body {
    set kv_leaf [$js_leaf bind_kv_bucket $leaf_mirror_bucket]
    set status [$kv_leaf status]
    assert {[dict get $status mirror_name] eq $hub_bucket}
    assert {[dict get $status mirror_domain] eq "hub"}
} -cleanup {
    execNatsCmd kv del $leaf_mirror_bucket -f {*}$leaf_auth
    execNatsCmd kv del $hub_bucket -f {*}$hub_auth
}


$js_leaf_to_hub destroy

$js_leaf destroy
$conn_leaf destroy

$js_hub destroy
$conn_hub destroy

stopNats NATS_LEAF
stopNats NATS_HUB

tcltest::cleanupTests
